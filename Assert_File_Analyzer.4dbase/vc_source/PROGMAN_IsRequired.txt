//%attributes = {"lang":"en"} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): jfletcher
// Date and time: 03/14/13, 15:07:33
// ----------------------------------------------------
// Description:
//   Goal: only display a progress bar if the operation
//   will take a "long" time.
//
//   This method estimates how long the operation will
//   take to complete.  It's designed to be called in
//   a loop.
//
//   It returns False if the operation will be fast
//   enough to skip the progress bar.
//   It returns True if the operation will take too long.
//
//   Input is completed amount of the current job and
//   the threshold.
//
//   Note the estimation is a simple linear calculation,
//   nothing fancy.
// --[HEADEREND]---------------------------------------
C_LONGINT($1;$pos_l)

C_BOOLEAN($0;$progressRequired_f)

C_LONGINT($threshold_l;$startTime_l;$currentTime_l;$estimatedDuration_l;$currentDuration_l)
C_REAL($progress_r)

$pos_l:=$1

$progress_r:=<>PROG_Progress_ar{$pos_l}

// Threshold for showing the progress bar, in ms.
// Change this value if you want the progress bar to be
// displayed sooner or later.
$threshold_l:=<>PROG_Threshold_al{$pos_l}

// This will be 0 for a new progress bar (see below).
$startTime_l:=<>PROG_StartTime_al{$pos_l}

$progressRequired_f:=False

// Set the start time the first time this method is called for this progress bar.
If ($startTime_l=0)
$startTime_l:=Milliseconds
<>PROG_StartTime_al{$pos_l}:=$startTime_l
$currentTime_l:=$startTime_l
Else 
// Get the current time.
$currentTime_l:=Milliseconds
End if 

// This is how long the job has been executing so far.
$currentDuration_l:=$currentTime_l-$startTime_l

// Duration estimation is nothing fancy, just a linear
// calculation based on how much is complete so far and
// how long it took.
If ($progress_r>0)
$estimatedDuration_l:=$currentDuration_l/$progress_r
Else 
$estimatedDuration_l:=0
End if 

// Test the estimate against the threshold.
If ($estimatedDuration_l>$threshold_l)
$progressRequired_f:=True
End if 

$0:=$progressRequired_f
