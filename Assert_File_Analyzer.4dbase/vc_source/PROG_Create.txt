//%attributes = {"lang":"en"} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 09/17/12, 17:09:33
// ----------------------------------------------------
// Description:
//   Create necessary data for progress bar.
//
//   The second parameter is used to dislay "x of y"
//   via Progress SET MESSAGE.
//
//   No, this method doesn't actually create a progress
//   bar, the manager module (PROG_MAN) does that.  This
//   is an optimization so that tight loops that are
//   updating a progress bar won't be slowed down by
//   calls to the 4D Progress component.
// --[HEADEREND]---------------------------------------
C_TEXT($1;$title_t)// Required, corresponds to the value used in Progress SET TITLE
C_LONGINT($2;$totalItems_l)// Optional, explained above.
C_LONGINT($3;$threshold_l)// Optional, used with Progress SET MESSAGE
C_LONGINT($4;$currentItem_l)// Optional, used with Progress SET MESSAGE
C_REAL($5;$progress_r)// Optional, used for Progress SET PROGRESS
C_TEXT($6;$message_t)// Optional, used with Progress SET MESSAGE

C_LONGINT($0;$newProgressID_l)// PROG_ id for the new progress bar; note this is NOT the 4D id

C_LONGINT($totalParams_l)

$title_t:=$1

$threshold_l:=0
$currentItem_l:=0
$totalItems_l:=0
$progress_r:=0
$message_t:=""

$totalParams_l:=Count parameters

If ($totalParams_l>=2)
$totalItems_l:=$2
If ($totalParams_l>=3)
$threshold_l:=$3
If ($totalParams_l>=4)
$currentItem_l:=$4
If ($totalParams_l>=5)
$progress_r:=$5
If ($totalParams_l>=6)
$message_t:=$6
End if 
End if 
End if 
End if 
End if 


While (Semaphore(<>PROG_Semaphore_t))
IDLE
End while 

// Add new progress bar data...
$newProgressID_l:=Max(<>PROG_IDs_al)+1

APPEND TO ARRAY(<>PROG_IDs_al;$newProgressID_l)

APPEND TO ARRAY(<>PROG_PIDs_al;Current process)
APPEND TO ARRAY(<>PROG_4DIDs_al;0)
APPEND TO ARRAY(<>PROG_Title_at;$title_t)
APPEND TO ARRAY(<>PROG_Threshold_al;$threshold_l)
APPEND TO ARRAY(<>PROG_Current_al;$currentItem_l)
APPEND TO ARRAY(<>PROG_Total_al;$totalItems_l)
APPEND TO ARRAY(<>PROG_Progress_ar;$progress_r)
APPEND TO ARRAY(<>PROG_Message_at;$message_t)
APPEND TO ARRAY(<>PROG_StartTime_al;0)
APPEND TO ARRAY(<>PROG_Close_af;False)

CLEAR SEMAPHORE(<>PROG_Semaphore_t)

$0:=$newProgressID_l
