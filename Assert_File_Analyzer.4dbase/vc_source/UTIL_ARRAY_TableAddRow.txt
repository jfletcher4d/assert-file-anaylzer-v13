//%attributes = {"lang":"en"} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 03/26/13, 15:11:49
// ----------------------------------------------------
// Description:
//   Generic code to append arrays onto tab-delimited
//   text. Array is appended as a row.
//
//   $1 should contain tab delimited text data with the
//   same number of rows as the array in $2 (this is not
//   checked, so you'll get wacky results if you don't
//   honor this).
// --[HEADEREND]---------------------------------------
C_POINTER($1;$TheTable_p_t)
C_POINTER($2;$pArray_p)

C_TEXT($colDelim_t;$rowDelim_t;$newCell_t)
C_LONGINT($i;$numCols_l;$rowType_l)

$TheTable_p_t:=$1
$pArray_p:=$2

$rowType_l:=Type($pArray_p->)

// Can't convert pictures to strings...
If ($rowType_l#Picture array )

$colDelim_t:=Char(Tab )
$rowDelim_t:=Char(Carriage return )
$numCols_l:=Size of array($pArray_p->)

For ($i;1;$numCols_l)

// Store the new cell value in a variable for easier reading.
$newCell_t:=String($pArray_p->{$i})

If ($TheTable_p_t->#"")
If ($i=1)
$TheTable_p_t->:=$TheTable_p_t->+$rowDelim_t
End if 
End if 

// Insert the new value.
$TheTable_p_t->:=$TheTable_p_t->+$newCell_t

If ($i<$numCols_l)
$TheTable_p_t->:=$TheTable_p_t->+$colDelim_t
End if 

End for 

End if 
