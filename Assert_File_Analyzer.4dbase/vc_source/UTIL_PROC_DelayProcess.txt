//%attributes = {"lang":"en"} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): jfletcher
// Date and time: 03/14/13, 11:07:22
// ----------------------------------------------------
// Description:
//   Wrapper for delay process.  The main additional
//   feature is that this method attempts to detect if
//   the database is quitting.  It does this by
//   checking to see if the process was acutally
//   delayed, or not.
//
//   When 4D is quitting, calls to DELAY PROCESS do
//   nothing, so the delay should be 0.
//
//   There is some risk that the process could
//   inadvertantly exit.  If a really small delay is
//   used, for example.  I'm optimistic this won't be
//   an issue.
//
// --[HEADEREND]---------------------------------------
C_LONGINT($1;$delay_l)// Optional, default is 1 second.
C_BOOLEAN($0;$DBIsQuitting_f)

C_LONGINT($start_l;$end_l;$total_l)

If (Count parameters>0)
$delay_l:=$1
Else 
$delay_l:=60
End if 

$start_l:=Tickcount
DELAY PROCESS(Current process;$delay_l)
$end_l:=Tickcount

$total_l:=$end_l-$start_l

// This may be too close of a comparison.  If the total is 0 might be better.
// But I worry about it being maybe 1 instead of 0.  In all my testing $total_l
// is always less than $delay_l, though I'm not sure if 4D guarantees this.
If ($total_l<$delay_l)
// Delay process is no longer working, most likely the database is quitting.
$DBIsQuitting_f:=True
Else 
$DBIsQuitting_f:=False
End if 

$0:=$DBIsQuitting_f